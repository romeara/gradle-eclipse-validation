apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'com.rsomeara'
version = '0.1-SNAPSHOT'

description = 'Gradle plug-in which allows configuration of Eclipse validation preferences'

sourceCompatibility = 1.6
targetCompatibility = 1.6

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy']
        }
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Eclipse Validation Plugin',
                   'Implementation-Version': version
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenLocal().url)
            pom.project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

eclipse {
	classpath {
        String containerDefinition = "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-${sourceCompatibility}"
		downloadSources=true
		containers = [ containerDefinition ]
	}
}

//Use the 2.3 wrapper so that gradleApi gets the 2.3 API jars
//Unfortunately this exposes us to issue GRADLE-1715 (https://issues.gradle.org/browse/GRADLE-1715)
task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
